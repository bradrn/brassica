categories
Nasl = m n ŋ
Stop = p t k ʔ
Fric = f s h
Aprx = w l j

C = Nasl Stop Fric Aprx

V = a ə i u
end

; presyllable syncope
ə / / # (C) _ ^V

; degemination
[Stop -ʔ] / Fric / _>

; metathesis
Aprx [C -Aprx] / \ / #_
[Nasl Aprx] Fric / \ / $_
ʔ C V / C V ʔ / #_
h C V / C V h / #_
C ʔ V / C V ʔ / #_
C h V / C V h / #_

; l-vocalisation
l / w / V (ʔ) _ // _ V
l / j / C_V

; vowel splitting
[a ə i u] / [ae e i ɨ] / _ ([ʔ h]) [n t s l j]
[a ə i u] / [a o ɨ u] / _ ([ʔ h]) [m p f w ŋ k j]

categories
feature Unassigned = ae e i ə ɨ a o u / High = aé é í ə́ ɨ́ á ó ú / Low = aè è ì ə̀ ɨ̀ à ò ù

V = ae e i ə ɨ a o u

; ‘h’ counts as voiced for the purpose of tonogenesis
Vcd = m n ŋ w l j h
end

; tonogenesis
[V +Unassigned] / [V +High] / _ [C -Vcd]
[V +Unassigned] / [V +Low]

; syllable structure simplification
Stop / / V_ // _V
C / / _ Aprx V
